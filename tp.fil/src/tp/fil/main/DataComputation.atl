-- @nsURI Java=http://www.eclipse.org/MoDisco/Java/0.2.incubation/java
-- @path Data=/tp.fil/src/tp/fil/resources/Data.ecore

module DataComputation;
create OUT : Data from IN : Java;

rule Model2Model {
	from
		s:Java!Model
	to
		t:Data!Model(
			name <- s.name,
			classes <- Java!ClassDeclaration.allInstances()
		)
}

rule Class2Class {
	from
		s:Java!ClassDeclaration
		(
			if (not s.package.oclIsUndefined()) then
				s.package.belongsToPetstoreModel()
			else false
			endif
		)
	to
		t:Data!Class(
			name <- s.name,
			methods <- s.bodyDeclarations
				->select(i | i.oclIsTypeOf(Java!MethodDeclaration)),
			attributes <- s.bodyDeclarations
				->select(i | i.oclIsTypeOf(Java!FieldDeclaration))
		)
}

rule Meth2Meth {
	from
		s:Java!MethodDeclaration
			(s.hasDefinedPackage())
	to
		t:Data!Method(
			name <- s.name,
			modifier <- s.modifier.visibility.toString(),
			type <- if s.returnType.type.name.indexOf('<') <> -1 then
						s.returnType.type.name
							.substring(1, s.returnType.type.name.indexOf('>'))
							.regexReplaceAll('<', ' of ')
							.regexReplaceAll('[^\\s]*\\.', '')
					else
						s.returnType.type.name.regexReplaceAll('[^\\s]*\\.', '')					
					endif,
			parameters <- s.parameters
		)
}

rule Field2Att {
	from
		s:Java!FieldDeclaration
			(s.hasDefinedPackage())
	to
		t:Data!Attribute(
			name <- s.fragments.first().name,
			modifier <- s.modifier.visibility.toString(),
			type <- if s.type.type.name.indexOf('<') <> -1 then
						s.type.type.name
							.substring(1, s.type.type.name.indexOf('>'))
							.regexReplaceAll('<', ' of ')
							.regexReplaceAll('[^\\s]*\\.', '')
					else
						s.type.type.name.regexReplaceAll('[^\\s]*\\.', '')
					endif
		)
}

rule Param2Param {
	from
		s:Java!SingleVariableDeclaration
			(s.hasDefinedPackage())
	to
		t:Data!Parameter(
			name <- s.name,
			type <- if s.type.type.name.indexOf('<') <> -1 then
						s.type.type.name
							.substring(1, s.type.type.name.indexOf('<'))
							.regexReplaceAll('<', ' of ')
							.regexReplaceAll('[^\\s]*\\.', '')
					else
						s.type.type.name.regexReplaceAll('[^\\s]*\\.', '')
					endif
		)
}

helper context Java!Package def: belongsToPetstoreModel(): Boolean =
    if(self.oclIsUndefined()) then
        false
    else
        if(self.package.oclIsUndefined()) then
            false
        else
            self.name.indexOf('model') <> -1 and self.package.name.indexOf('petstore') <> -1
        endif
    endif;

helper context Java!BodyDeclaration def: hasDefinedPackage(): Boolean =
    if (not self.originalCompilationUnit.oclIsUndefined()) then
		if (not self.originalCompilationUnit.package.oclIsUndefined()) then
			self.originalCompilationUnit.package.belongsToPetstoreModel()
		else false
		endif
	else false
	endif;

helper context Java!SingleVariableDeclaration def: hasDefinedPackage(): Boolean =
    if (not self.originalCompilationUnit.oclIsUndefined()) then
		if (not self.originalCompilationUnit.package.oclIsUndefined()) then
			self.originalCompilationUnit.package.belongsToPetstoreModel()
		else false
		endif
	else false
	endif;